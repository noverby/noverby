#!/usr/bin/env -S nu --stdin
def main [] {
    # Only parse the first 100 lines of input
    let input = $in | tail -n 100
    if ($input | is-empty) {
        print "Please provide input through a pipe"
        exit 1
    }

    let inputs = $input | split row -r '\s+';

    let paths = ($inputs
      | each { |input|
          if ($input | is-empty) {
              $input
          } else if ($input | split chars | get 0 | $in in ["'" '"' '`']) {
              $input | str substring 1..(-2)
          } else {
              $input
          }
      }
        | each { str replace -r '^~' $env.HOME }
        | where { |input|
          ($input | path exists) and ($input not-in ["/" "."])
        }
    )

    let urls = $inputs | reduce --fold [] { |it, acc|
        let match = $it | parse --regex '\(?((https?|ftp):\/\/[^\s/$.?#].[^\s]*?)(?:\)|(?=\s|$))' | get capture0;
        if ($match | is-empty) { $acc } else { $acc ++ [ $match.0 ] }
    }

    let selected = $paths ++ $urls | uniq | str join "\n" | sk

    if (($selected | path type) == "file" or ($selected | readlink $in | path type) == "file") {
      xdg-open $selected | complete
      return "."
    }
    try {
      $selected | url parse
      xdg-open $selected | complete
      return "."
    }
}
