build:
  mkdir -p build
  rm -f build/out.cwasm
  mojo build --emit llvm --target-features "none" -o build/out.ll src/main.mojo
  sed -i '/call void @llvm\.lifetime\.\(start\|end\)/d' build/out.ll
  llc --mtriple=wasm64-wasi -filetype=obj build/out.ll
  wasm-ld --no-entry --export-all --allow-undefined -mwasm64 --initial-memory=268435456 -o build/out.wasm build/out.o

# Incremental build — skip if out.wasm is newer than all source files.
build-if-changed:
  #!/usr/bin/env bash
  set -euo pipefail
  mkdir -p build
  if [ -f build/out.wasm ] && [ -z "$(find src/ -name '*.mojo' -newer build/out.wasm 2>/dev/null)" ]; then
    echo "build: out.wasm is up-to-date, skipping."
  else
    just build
  fi

precompile: build
  mojo run -I ../wasmtime-mojo/src scripts/precompile.mojo

# Incremental precompile — skip if cwasm is newer than wasm.
precompile-if-changed: build-if-changed
  #!/usr/bin/env bash
  set -euo pipefail
  if [ -f build/out.cwasm ] && [ build/out.cwasm -nt build/out.wasm ]; then
    echo "precompile: out.cwasm is up-to-date, skipping."
  else
    just precompile
  fi

# Build precompiled test binaries (parallel, incremental).
# Depends on precompile-if-changed so .cwasm exists for fast WASM loading.
# Pass optional filter args to build only matching modules:
#   just test-build signals       # build only test_signals
#   just test-build signals mut   # build test_signals + test_mutations
test-build *FILTER: precompile-if-changed
  bash scripts/build_test_binaries.sh {{FILTER}}

# Run precompiled test binaries (parallel, ~10s).
# Pass optional filter args to run only matching modules:
#   just test-run signals         # run only test_signals
#   just test-run -v dsl          # verbose, only test_dsl
test-run *FILTER:
  bash scripts/run_test_binaries.sh {{FILTER}}

# Build + run precompiled test binaries (fastest full Mojo test cycle).
# Pass optional filter args to target specific modules:
#   just test signals             # build + run only test_signals
#   just test signals mutations   # build + run test_signals + test_mutations
test *FILTER: (test-build FILTER) (test-run FILTER)

test-js: build
  deno run --allow-read test-js/run.ts

test-all: test test-js

serve: build
  @echo "Open http://localhost:4507/examples/counter/"
  @echo "Open http://localhost:4507/examples/todo/"
  @echo "Open http://localhost:4507/examples/bench/"
  deno run --allow-net --allow-read jsr:@std/http/file-server
